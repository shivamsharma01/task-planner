plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.github.node-gradle.node' version '7.0.1'
}

group = 'com.nagp'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

node {
	version = '20.19.4'
	npmVersion = '10.8.2'
	download = true
}

tasks.register('buildAngular', NpmTask) {
	dependsOn npmInstall
	args = ['run', 'build']
	workingDir = file('task-planner-ui')
}


tasks.register('copyAngularToStatic') {
	doFirst {
		copy {
			from 'task-planner-ui/browser/media'
			into 'src/main/resources/static'
		}
		copy {
			from 'src/main/resources/static/browser'
			into 'src/main/resources/static'
		}
	}
	doLast {
		delete 'src/main/resources/static/browser'
		delete 'src/main/resources/static/prerendered-routes.json'
		delete 'src/main/resources/static/3rdpartylicenses.txt'
	}
	dependsOn buildAngular
}

processResources {
	dependsOn copyAngularToStatic
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}
